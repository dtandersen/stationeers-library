<?xml version="1.0" encoding="utf-8"?>
<InstructionData xmlns:xsd="http://www.w3.org/2001/XMLSchema" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
  <DateTime>132968890070583878</DateTime>
  <GameVersion>0.2.3262.16260</GameVersion>
  <Title>SMARTSUIT</Title>
  <Description>â€‹</Description>
  <Author>onebit</Author>
  <WorkshopFileHandle>0</WorkshopFileHandle>
  <Instructions>define MINO2PP 25
define MAXTEMP 50
define MINTEMP 10
define MAXTOXIN 0.45
define MAXNO2 4.5
define CTOK 273.15

alias Helmet d0
alias Jetpack d1

alias suitpres r0
alias suitppo2 r1
alias ratio r2
alias open r3
alias extpres r4
alias presbad r5
alias tempbad r6
alias exttemp r7
alias hazard r9
alias autoclosed r10
alias temp1 r11
alias pp r12

loop:
l suitpres db Pressure
l ratio db RatioOxygen
mul suitppo2 ratio suitpres
slt open suitppo2 MINO2PP
s db Filtration open

l open Helmet Open
not temp1 open
# remain autoclosed if helmet is not open
and autoclosed autoclosed temp1
jal checkhazard
beq open 0 checkopen
beq open 1 checkclose
done:
yield
j loop

checkopen:
# don't open helmet if manually opened
beq autoclosed 0 done
# don't open helmet if hazardous
beq hazard 1 done
s Helmet Open 1
move autoclosed 0
j done

checkclose:
beq hazard 1 autoclose
j done

checkhazard:
#close if o2 is too low
l extpres db PressureExternal
l ratio db RatioOxygen
mul pp ratio extpres
blt pp MINO2PP hazarddetected

#close if toxins are present
l ratio db RatioPollutant
l temp1 db RatioVolatiles
add ratio ratio temp1
mul pp ratio extpres
bge pp MAXTOXIN hazarddetected

# close if no2 is present
l ratio db RatioNitrousOxide
mul pp ratio extpres
bge pp MAXNO2 hazarddetected

#close if temp is extreme
l exttemp db TemperatureExternal
add temp1 MAXTEMP CTOK
bge exttemp temp1 hazarddetected
add temp1 MINTEMP CTOK
ble exttemp temp1 hazarddetected

move hazard 0
j ra

hazarddetected:
move hazard 1
j ra

autoclose:
s Helmet Open 0
move autoclosed 1
j done</Instructions>
</InstructionData>