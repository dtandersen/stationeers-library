<?xml version="1.0" encoding="utf-8"?>
<InstructionData xmlns:xsd="http://www.w3.org/2001/XMLSchema" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
  <DateTime>132968890070583878</DateTime>
  <GameVersion>0.2.3262.16260</GameVersion>
  <Title>SMARTSUIT</Title>
  <Description>â€‹</Description>
  <Author>onebit</Author>
  <WorkshopFileHandle>0</WorkshopFileHandle>
  <Instructions>define MINO2PP 25
define MAXTEMP 50
define MINTEMP 10
define MAXPRES 125
define MINPRES 80
define MAXTOXIN 0.45
define MAXNO2 4.5
define CTOK 273.15

alias Suit db
alias Helmet d0
alias Jetpack d1

alias pressure r0
alias manualopened r1
alias manualclosed r2
alias ratio r3
alias open r4
alias stayclosed r5
alias temperature r6
alias exppos r7 # expected helmet position
alias oop r8 # helmet out of position
alias temp1 r9
alias pp r10
alias extreme r11
alias alert r12

s Helmet Volume 50
loop:
l pressure Suit Pressure
l ratio Suit RatioOxygen
mul pp ratio pressure
slt open pp MINO2PP
s Suit Filtration open
jal checkhazard
l open Helmet Open
beq open 0 skipalert
s Helmet SoundAlert alert
skipalert:
not temp1 open
s Suit AirRelease temp1
# remain autoclosed if helmet is not open
sne oop exppos open
and manualclosed oop temp1
and manualopened oop open
beqal manualopened 1 manuallyopened
beqal manualclosed 1 manuallyclosed
beq open 0 checkopen
beq open 1 checkclose
done:
yield
j loop
manuallyopened:
s Helmet SoundAlert 20
move exppos 1
move stayclosed 0
j ra
manuallyclosed:
s Helmet SoundAlert 21
move exppos 0
move stayclosed 1
j ra
checkopen:
beq stayclosed 1 done
beq extreme 1 done
s Helmet SoundAlert 20
s Helmet Open 1
move exppos 1
j done
checkclose:
beq extreme 1 autoclose
beq stayclosed 1 autoclose
j done
checkhazard:
move alert 0
#close if toxins are present
l ratio Suit RatioPollutant
l temp1 Suit RatioVolatiles
add ratio ratio temp1
mul pp ratio pressure
bge pp MAXTOXIN hazarddetected
# close if no2 is present
l ratio Suit RatioNitrousOxide
mul pp ratio pressure
bge pp MAXNO2 hazarddetected
# close if pressure is extreme
l pressure Suit PressureExternal
bge pressure MAXPRES preshigh
ble pressure MINPRES preslow
# close if temp is extreme
l temperature Suit TemperatureExternal
add temp1 MAXTEMP CTOK
bge temperature temp1 temphigh
add temp1 MINTEMP CTOK
ble temperature temp1 templow
move extreme 0
j ra
preshigh:
move alert 39
j extremeenv
preslow:
move alert 40
j extremeenv
templow:
move alert 42
j extremeenv
temphigh:
move alert 41
j extremeenv
extremeenv:
move extreme 1
j ra
hazarddetected:
move alert 43
move stayclosed 1
j ra
autoclose:
s Helmet Open 0
move exppos 0
j done</Instructions>
</InstructionData>