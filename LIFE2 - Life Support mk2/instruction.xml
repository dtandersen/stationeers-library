<?xml version="1.0" encoding="utf-8"?>
<InstructionData xmlns:xsd="http://www.w3.org/2001/XMLSchema" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
  <DateTime>132016852881624573</DateTime>
  <GameVersion>0.2.1995.9104</GameVersion>
  <Title>LIFE2 - Life Support mk2</Title>
  <Description />
  <Author>onebit(warg)</Author>
  <WorkshopFileHandle>0</WorkshopFileHandle>
  <Instructions># Life Support 1.0 https://github.com/dtandersen/sta
alias GasSensor d0
alias O2Valve d1
alias CO2Valve d2
alias N2Valve d3
alias Unused d4
alias Unused d5

alias device r0 # reserved
alias partial r1 # partial pressure of element
alias ratio r2 # ratio of element
alias pressure r3 # room pressure
alias poweron r4
alias o2Ok r5
alias temp1 r9
alias temp2 r10
alias temp3 r11
alias sp r13
alias error r14
alias rate r15

define maxPressure 100
define o2Target 0.20
define co2Target 0.02
define nTarget 0.78
define kp 0.2
#s GasSensor On 1

start:
yield

l pressure GasSensor Pressure
s db Setting pressure

# oxygen regulation
move r0 1
mul sp o2Target maxPressure
l ratio GasSensor RatioOxygen
div o2Ok ratio o2Target
div o2Ok o2Ok 3.0
jal calcpid
jal setpump

# co2 regulation
move r0 2
l ratio GasSensor RatioCarbonDioxide
###
l temp1 GasSensor RatioNitrogen # read N
sub temp1 nTarget temp1         # missing N = target - actual
sub temp1 temp1 co2Target
max temp1 temp1 0               # missing N = max(n, 0)
add temp1 co2Target temp1       # co2Target = co2Target + missingN
###
mul sp temp1 maxPressure        # sp = co2Target * pressure
jal calcpid
mul rate rate o2Ok
jal setpump

# nitrogen regulation
move r0 3
mul sp nTarget maxPressure # pressure setpoint
l ratio GasSensor RatioNitrogen
jal calcpid
mul rate rate o2Ok
jal setpump
j start

calcpid:
move partial 0
beqz pressure nopressure # global pressure
mul partial pressure ratio # global ratio
nopressure:
sub error sp partial
mul rate kp error # global rate
j ra

setpump:
sgtz temp1 rate
slt temp2 pressure maxPressure
and poweron temp1 temp2
s dr0 On poweron
s dr0 Setting rate
j ra</Instructions>
</InstructionData>